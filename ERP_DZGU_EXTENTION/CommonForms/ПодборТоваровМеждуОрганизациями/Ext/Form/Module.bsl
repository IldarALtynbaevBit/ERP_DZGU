

&НаСервере
&Вместо("ЗаполнитьТаблицуТоваров")
Процедура Расш1_ЗаполнитьТаблицуТоваров()
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	Товары.Номенклатура КАК Номенклатура,
	                      |	Товары.Характеристика КАК Характеристика,
	                      |	Товары.Серия КАК Серия,
	                      |	Товары.Количество КАК Количество,
	                      |	Товары.Назначение КАК Назначение
	                      |ПОМЕСТИТЬ Товары
	                      |ИЗ
	                      |	&Товары КАК Товары
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	Остатки.Номенклатура КАК Номенклатура,
	                      |	Остатки.Характеристика КАК Характеристика,
	                      |	Остатки.Серия КАК Серия,
	                      |	Остатки.Назначение КАК Назначение,
	                      |	СУММА(Остатки.КоличествоОстаток) КАК КоличествоОстаток
	                      |ПОМЕСТИТЬ ОстаткиОтправителя
	                      |ИЗ
	                      |	(ВЫБРАТЬ
	                      |		Остатки.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	                      |		Остатки.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	                      |		Остатки.АналитикаУчетаНоменклатуры.Серия КАК Серия,
	                      |		Остатки.АналитикаУчетаНоменклатуры.Назначение КАК Назначение,
	                      |		СУММА(Остатки.КоличествоОстаток) КАК КоличествоОстаток
	                      |	ИЗ
	                      |		РегистрНакопления.ТоварыОрганизаций.Остатки(
	                      |				&Граница,
	                      |				Организация = &ОрганизацияОтправитель
	                      |					И АналитикаУчетаНоменклатуры.МестоХранения = &Склад
	                      |					И ВидЗапасов.ТипЗапасов В (&ДоступныеТипыЗапасов)
	                      |					И (ВидЗапасов.ВладелецТовара = &ВладелецТовара
	                      |						ИЛИ &ПоВсемВладельцам)) КАК Остатки
	                      |	ГДЕ
	                      |		НЕ(Остатки.АналитикаУчетаНоменклатуры.Номенклатура.ПодакцизныйТовар
	                      |					И &СкрыватьПодакцизныеТовары)
	                      |	
	                      |	СГРУППИРОВАТЬ ПО
	                      |		Остатки.АналитикаУчетаНоменклатуры.Номенклатура,
	                      |		Остатки.АналитикаУчетаНоменклатуры.Характеристика,
	                      |		Остатки.АналитикаУчетаНоменклатуры.Серия,
	                      |		Остатки.АналитикаУчетаНоменклатуры.Назначение
	                      |	
	                      |	ОБЪЕДИНИТЬ ВСЕ
	                      |	
	                      |	ВЫБРАТЬ
	                      |		Остатки.АналитикаУчетаНоменклатуры.Номенклатура,
	                      |		Остатки.АналитикаУчетаНоменклатуры.Характеристика,
	                      |		Остатки.АналитикаУчетаНоменклатуры.Серия,
	                      |		Остатки.АналитикаУчетаНоменклатуры.Назначение,
	                      |		СУММА(Остатки.КоличествоОстаток)
	                      |	ИЗ
	                      |		РегистрНакопления.РезервыТоваровОрганизаций.Остатки(
	                      |				&Граница,
	                      |				Организация = &ОрганизацияОтправитель
	                      |					И АналитикаУчетаНоменклатуры.МестоХранения = &Склад
	                      |					И ВидЗапасов.ТипЗапасов В (&ДоступныеТипыЗапасов)
	                      |					И (ВидЗапасов.ВладелецТовара = &ВладелецТовара
	                      |						ИЛИ &ПоВсемВладельцам)) КАК Остатки
	                      |	ГДЕ
	                      |		НЕ(Остатки.АналитикаУчетаНоменклатуры.Номенклатура.ПодакцизныйТовар
	                      |					И &СкрыватьПодакцизныеТовары)
	                      |	
	                      |	СГРУППИРОВАТЬ ПО
	                      |		Остатки.АналитикаУчетаНоменклатуры.Номенклатура,
	                      |		Остатки.АналитикаУчетаНоменклатуры.Характеристика,
	                      |		Остатки.АналитикаУчетаНоменклатуры.Серия,
	                      |		Остатки.АналитикаУчетаНоменклатуры.Назначение) КАК Остатки
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	Остатки.Номенклатура,
	                      |	Остатки.Характеристика,
	                      |	Остатки.Серия,
	                      |	Остатки.Назначение
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	Остатки.Номенклатура КАК Номенклатура,
	                      |	Остатки.Характеристика КАК Характеристика,
	                      |	Остатки.Серия КАК Серия,
	                      |	Остатки.Назначение КАК Назначение,
	                      |	СУММА(Остатки.КоличествоОстаток) КАК КоличествоОстаток
	                      |ПОМЕСТИТЬ ОстаткиПолучателя
	                      |ИЗ
	                      |	(ВЫБРАТЬ
	                      |		Остатки.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	                      |		Остатки.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	                      |		Остатки.АналитикаУчетаНоменклатуры.Серия КАК Серия,
	                      |		Остатки.АналитикаУчетаНоменклатуры.Назначение КАК Назначение,
	                      |		СУММА(Остатки.КоличествоОстаток) КАК КоличествоОстаток
	                      |	ИЗ
	                      |		РегистрНакопления.ТоварыОрганизаций.Остатки(
	                      |				&Граница,
	                      |				&ОтрицательныеОстатки
	                      |					И Организация = &ОрганизацияПолучатель
	                      |					И АналитикаУчетаНоменклатуры.МестоХранения = &Склад) КАК Остатки
	                      |	ГДЕ
	                      |		Остатки.КоличествоОстаток < 0
	                      |		И НЕ(Остатки.АналитикаУчетаНоменклатуры.Номенклатура.ПодакцизныйТовар
	                      |					И &СкрыватьПодакцизныеТовары)
	                      |		И Остатки.АналитикаУчетаНоменклатуры.Номенклатура.ОблагаетсяНДСУПокупателя = &ОблагаетсяНДСУПокупателя
	                      |	
	                      |	СГРУППИРОВАТЬ ПО
	                      |		Остатки.АналитикаУчетаНоменклатуры.Номенклатура,
	                      |		Остатки.АналитикаУчетаНоменклатуры.Характеристика,
	                      |		Остатки.АналитикаУчетаНоменклатуры.Серия,
	                      |		Остатки.АналитикаУчетаНоменклатуры.Назначение
	                      |	
	                      |	ОБЪЕДИНИТЬ ВСЕ
	                      |	
	                      |	ВЫБРАТЬ
	                      |		Остатки.АналитикаУчетаНоменклатуры.Номенклатура,
	                      |		Остатки.АналитикаУчетаНоменклатуры.Характеристика,
	                      |		Остатки.АналитикаУчетаНоменклатуры.Серия,
	                      |		Остатки.АналитикаУчетаНоменклатуры.Назначение,
	                      |		СУММА(Остатки.КоличествоОстаток)
	                      |	ИЗ
	                      |		РегистрНакопления.РезервыТоваровОрганизаций.Остатки(
	                      |				&Граница,
	                      |				&ОтрицательныеОстатки
	                      |					И Организация = &ОрганизацияПолучатель
	                      |					И АналитикаУчетаНоменклатуры.МестоХранения = &Склад) КАК Остатки
	                      |	ГДЕ
	                      |		НЕ(Остатки.АналитикаУчетаНоменклатуры.Номенклатура.ПодакцизныйТовар
	                      |					И &СкрыватьПодакцизныеТовары)
	                      |		И Остатки.АналитикаУчетаНоменклатуры.Номенклатура.ОблагаетсяНДСУПокупателя = &ОблагаетсяНДСУПокупателя
	                      |	
	                      |	СГРУППИРОВАТЬ ПО
	                      |		Остатки.АналитикаУчетаНоменклатуры.Номенклатура,
	                      |		Остатки.АналитикаУчетаНоменклатуры.Характеристика,
	                      |		Остатки.АналитикаУчетаНоменклатуры.Серия,
	                      |		Остатки.АналитикаУчетаНоменклатуры.Назначение) КАК Остатки
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	Остатки.Номенклатура,
	                      |	Остатки.Характеристика,
	                      |	Остатки.Серия,
	                      |	Остатки.Назначение
	                      |
	                      |ИМЕЮЩИЕ
	                      |	СУММА(Остатки.КоличествоОстаток) < 0
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ВЫБОР
	                      |		КОГДА Товары.Количество ЕСТЬ NULL
	                      |				ИЛИ Товары.Количество = 0
	                      |			ТОГДА ЛОЖЬ
	                      |		ИНАЧЕ ИСТИНА
	                      |	КОНЕЦ КАК Выбран,
	                      |	ОстаткиПолучателя.Номенклатура КАК Номенклатура,
	                      |	ОстаткиПолучателя.Характеристика КАК Характеристика,
	                      |	ОстаткиПолучателя.Серия КАК Серия,
	                      |	ОстаткиПолучателя.Назначение КАК Назначение,
	                      |	СУММА(ОстаткиОтправителя.КоличествоОстаток) КАК КоличествоОстатокОтправителя,
	                      |	СУММА(ОстаткиПолучателя.КоличествоОстаток) КАК КоличествоОстатокПолучателя,
	                      |	СУММА(ВЫБОР
	                      |			КОГДА Товары.Количество ЕСТЬ NULL
	                      |					ИЛИ Товары.Количество = 0
	                      |				ТОГДА ВЫБОР
	                      |						КОГДА -ОстаткиПолучателя.КоличествоОстаток < ОстаткиОтправителя.КоличествоОстаток
	                      |							ТОГДА -ОстаткиПолучателя.КоличествоОстаток
	                      |						ИНАЧЕ ОстаткиОтправителя.КоличествоОстаток
	                      |					КОНЕЦ
	                      |			ИНАЧЕ Товары.Количество
	                      |		КОНЕЦ) КАК Количество
	                      |ПОМЕСТИТЬ ВТ_Типовая
	                      |ИЗ
	                      |	ОстаткиПолучателя КАК ОстаткиПолучателя
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ Товары КАК Товары
	                      |		ПО ОстаткиПолучателя.Номенклатура = Товары.Номенклатура
	                      |			И ОстаткиПолучателя.Характеристика = Товары.Характеристика
	                      |			И ОстаткиПолучателя.Серия = Товары.Серия
	                      |			И ОстаткиПолучателя.Назначение = Товары.Назначение
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиОтправителя КАК ОстаткиОтправителя
	                      |		ПО ОстаткиПолучателя.Номенклатура = ОстаткиОтправителя.Номенклатура
	                      |			И ОстаткиПолучателя.Характеристика = ОстаткиОтправителя.Характеристика
	                      |			И ОстаткиПолучателя.Серия = ОстаткиОтправителя.Серия
	                      |			И ОстаткиПолучателя.Назначение = ОстаткиОтправителя.Назначение
	                      |ГДЕ
	                      |	&ОтрицательныеОстатки
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ВЫБОР
	                      |		КОГДА Товары.Количество ЕСТЬ NULL
	                      |				ИЛИ Товары.Количество = 0
	                      |			ТОГДА ЛОЖЬ
	                      |		ИНАЧЕ ИСТИНА
	                      |	КОНЕЦ,
	                      |	ОстаткиПолучателя.Номенклатура,
	                      |	ОстаткиПолучателя.Характеристика,
	                      |	ОстаткиПолучателя.Серия,
	                      |	ОстаткиПолучателя.Назначение
	                      |
	                      |ИМЕЮЩИЕ
	                      |	СУММА(ВЫБОР
	                      |			КОГДА Товары.Количество ЕСТЬ NULL
	                      |					ИЛИ Товары.Количество = 0
	                      |				ТОГДА ВЫБОР
	                      |						КОГДА -ОстаткиПолучателя.КоличествоОстаток < ОстаткиОтправителя.КоличествоОстаток
	                      |							ТОГДА -ОстаткиПолучателя.КоличествоОстаток
	                      |						ИНАЧЕ ОстаткиОтправителя.КоличествоОстаток
	                      |					КОНЕЦ
	                      |			ИНАЧЕ Товары.Количество
	                      |		КОНЕЦ) <> 0
	                      |
	                      |ОБЪЕДИНИТЬ ВСЕ
	                      |
	                      |ВЫБРАТЬ
	                      |	ВЫБОР
	                      |		КОГДА Товары.Количество ЕСТЬ NULL
	                      |				ИЛИ Товары.Количество = 0
	                      |			ТОГДА ЛОЖЬ
	                      |		ИНАЧЕ ИСТИНА
	                      |	КОНЕЦ,
	                      |	ОстаткиОтправителя.Номенклатура,
	                      |	ОстаткиОтправителя.Характеристика,
	                      |	ОстаткиОтправителя.Серия,
	                      |	ОстаткиОтправителя.Назначение,
	                      |	СУММА(ОстаткиОтправителя.КоличествоОстаток),
	                      |	0,
	                      |	СУММА(ВЫБОР
	                      |			КОГДА Товары.Количество ЕСТЬ NULL
	                      |					ИЛИ Товары.Количество = 0
	                      |				ТОГДА ОстаткиОтправителя.КоличествоОстаток
	                      |			ИНАЧЕ Товары.Количество
	                      |		КОНЕЦ)
	                      |ИЗ
	                      |	ОстаткиОтправителя КАК ОстаткиОтправителя
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ Товары КАК Товары
	                      |		ПО ОстаткиОтправителя.Номенклатура = Товары.Номенклатура
	                      |			И ОстаткиОтправителя.Характеристика = Товары.Характеристика
	                      |			И ОстаткиОтправителя.Серия = Товары.Серия
	                      |			И ОстаткиОтправителя.Назначение = Товары.Назначение
	                      |ГДЕ
	                      |	НЕ &ОтрицательныеОстатки
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ВЫБОР
	                      |		КОГДА Товары.Количество ЕСТЬ NULL
	                      |				ИЛИ Товары.Количество = 0
	                      |			ТОГДА ЛОЖЬ
	                      |		ИНАЧЕ ИСТИНА
	                      |	КОНЕЦ,
	                      |	ОстаткиОтправителя.Номенклатура,
	                      |	ОстаткиОтправителя.Характеристика,
	                      |	ОстаткиОтправителя.Серия,
	                      |	ОстаткиОтправителя.Назначение
	                      |
	                      |ИМЕЮЩИЕ
	                      |	СУММА(ВЫБОР
	                      |			КОГДА Товары.Количество ЕСТЬ NULL
	                      |					ИЛИ Товары.Количество = 0
	                      |				ТОГДА ОстаткиОтправителя.КоличествоОстаток
	                      |			ИНАЧЕ Товары.Количество
	                      |		КОНЕЦ) <> 0
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	БИТ_КарантинОбороты.Номенклатура КАК Номенклатура,
	                      |	БИТ_КарантинОбороты.Серия КАК Серия,
	                      |	БИТ_КарантинОбороты.КоличествоРасход КАК Сертифицировано,
	                      |	БИТ_КарантинОбороты.КоличествоОборот КАК ОстатокНеСертифицирован
	                      |ПОМЕСТИТЬ ВТ_Сертификация
	                      |ИЗ
	                      |	РегистрНакопления.БИТ_Карантин.Обороты(
	                      |			,
	                      |			,
	                      |			,
	                      |			(Номенклатура, Серия, Склад) В
	                      |				(ВЫБРАТЬ
	                      |					ВТ_Типовая.Номенклатура КАК Номенклатура,
	                      |					ВТ_Типовая.Серия КАК Серия,
	                      |					&Склад КАК Склад
	                      |				ИЗ
	                      |					ВТ_Типовая КАК ВТ_Типовая)) КАК БИТ_КарантинОбороты
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ВТ_Типовая.Выбран КАК Выбран,
	                      |	ВТ_Типовая.Номенклатура КАК Номенклатура,
	                      |	ВТ_Типовая.Характеристика КАК Характеристика,
	                      |	ВТ_Типовая.Серия КАК Серия,
	                      |	ВТ_Типовая.Назначение КАК Назначение,
	                      |	ВТ_Типовая.КоличествоОстатокОтправителя КАК КоличествоОстатокОтправителя,
	                      |	ВТ_Типовая.КоличествоОстатокПолучателя КАК КоличествоОстатокПолучателя,
	                      |	ВТ_Типовая.Количество КАК Количество,
	                      |	ВТ_Сертификация.Сертифицировано КАК Сертифицировано,
	                      |	ВТ_Сертификация.ОстатокНеСертифицирован КАК ОстатокНеСертифицирован
	                      |ИЗ
	                      |	ВТ_Типовая КАК ВТ_Типовая
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Сертификация КАК ВТ_Сертификация
	                      |		ПО ВТ_Типовая.Номенклатура = ВТ_Сертификация.Номенклатура
	                      |			И ВТ_Типовая.Серия = ВТ_Сертификация.Серия");
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ОрганизацияПолучатель", ОрганизацияПолучатель);
	Запрос.УстановитьПараметр("ОрганизацияОтправитель", Организация);
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("ОтрицательныеОстатки", ОтрицательныеОстатки);
	Запрос.УстановитьПараметр("СкрыватьПодакцизныеТовары", СкрыватьПодакцизныеТовары);
	Запрос.УстановитьПараметр("ОблагаетсяНДСУПокупателя", ОблагаетсяНДСУПокупателя);
	
	ДоступныеТипыЗапасов = Новый Массив();
	ВладелецТовара = Неопределено;
	Если ЭтоКомиссия И ЭтоВозврат Тогда
		ДоступныеТипыЗапасов.Добавить(Перечисления.ТипыЗапасов.КомиссионныйТовар);
		ВладелецТовара = ОрганизацияПолучатель;
	Иначе
		ДоступныеТипыЗапасов.Добавить(Перечисления.ТипыЗапасов.КомиссионныйТовар);
		ДоступныеТипыЗапасов.Добавить(Перечисления.ТипыЗапасов.Товар);
		ДоступныеТипыЗапасов.Добавить(Перечисления.ТипыЗапасов.ТоварНаХраненииСПравомПродажи);
	КонецЕсли;
	Запрос.УстановитьПараметр("ДоступныеТипыЗапасов", ДоступныеТипыЗапасов);
	Запрос.УстановитьПараметр("ВладелецТовара", ВладелецТовара);
	Запрос.УстановитьПараметр("ПоВсемВладельцам", Не ЗначениеЗаполнено(ВладелецТовара));
	
	ДатаЗаполнения = ?(ЗначениеЗаполнено(Дата), КонецДня(Дата), ТекущаяДатаСеанса());
	Граница = Новый Граница(ДатаЗаполнения, ВидГраницы.Включая);
	Запрос.УстановитьПараметр("Граница", Граница);
	
	Если ТаблицаТоваров.Количество() > 0 Тогда
		Товары = ТаблицаТоваров.Выгрузить(, "Выбран, Номенклатура, Характеристика, Серия, Назначение, Количество");
		СтрокиКУдалению = Товары.НайтиСтроки(Новый Структура("Выбран", Ложь));
		Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
			Товары.Удалить(СтрокаКУдалению);
		КонецЦикла;
	Иначе
		Товары = ПолучитьИзВременногоХранилища(АдресВХранилище);
		Товары.Свернуть("Номенклатура, Характеристика, Серия, Назначение", "Количество");
	КонецЕсли;
	Запрос.УстановитьПараметр("Товары", Товары);
	
	ПолучательУказан = ЗначениеЗаполнено(ОрганизацияПолучатель);
	ИспользоватьКомиссию = (ЭтоКомиссия И ПолучательУказан);
	ИспользоватьПродажу = (ЭтоПродажа И ПолучательУказан);
	
	Запрос.УстановитьПараметр("ИспользоватьКомиссию", ИспользоватьКомиссию);
	Запрос.УстановитьПараметр("ИспользоватьПродажу", ИспользоватьПродажу);
	Запрос.УстановитьПараметр("КомиссияПриЗакупках", ИспользоватьКомиссию И ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриЗакупках"));
	Запрос.УстановитьПараметр("ЗапасыПоПоставщикам", ИспользоватьПродажу И ПолучитьФункциональнуюОпцию("ФормироватьВидыЗапасовПоПоставщикам"));
	
	Результат = Запрос.Выполнить();
	ТаблицаТоваров.Загрузить(Результат.Выгрузить());
	
КонецПроцедуры  


&НаКлиенте
Процедура Расш1_ТаблицаТоваровВыбранПриИзмененииПосле(Элемент)
    ТекСтр = Элементы.ТаблицаТоваров.ТекущиеДанные;
    Если ТекСтр.Количество>ТекСтр.Сертифицировано И ТекСтр.Выбран Тогда
        
        Оповещение = Новый ОписаниеОповещения("ПослеОтветаНаВопрос", ЭтотОбъект, ТекСтр);
        

        ПоказатьВопрос(Оповещение, "Нехватает сертифицированной номенклатуры "
        + ТекСтр.Номенклатура + "в количестве " + Строка(ТекСтр.Количество-ТекСтр.Сертифицировано)
        +". Все равно выбрать?",        
        РежимДиалогаВопрос.ДаНетОтмена,
        0,          
        КодВозвратаДиалога.Нет,
        "Сертификация отсутствует."         
        );
        
    КонецЕсли; 
КонецПроцедуры
&НаКлиенте

Процедура ПослеОтветаНаВопрос(Результат, Параметры) Экспорт     
    Если Результат = КодВозвратаДиалога.Да Тогда
        Параметры.Выбран = Истина;
    Иначе
        Параметры.Выбран = Ложь;
    КонецЕсли;
КонецПроцедуры


&НаСервере
&После("УстановитьУсловноеОформление")
Процедура Расш1_УстановитьУсловноеОформление()
    Элементы.ТаблицаТоваров.ЧередованиеЦветовСтрок = Ложь;
    Элемент = УсловноеОформление.Элементы.Добавить();
    
    ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
    ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаТоваров.Имя);

    ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
    ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаТоваров.Количество");
    ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Больше;
    ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаТоваров.Сертифицировано");

    Элемент.Оформление.УстановитьЗначениеПараметра("ЦветФона",WebЦвета.Розовый);
КонецПроцедуры

